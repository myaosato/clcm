(defpackage :clcm/test/main
  (:use :cl)
  (:nicknames :clcm/test)
  (:shadow)
  (:import-from :clcm/clcm :cm->html)
  (:import-from :asdf :system-source-directory)
  (:import-from :cl-json :decode-json-from-source)
  (:export :test
           :test-for
           :test-range
           :test-section))
(in-package :clcm/test/main)

(defvar *spec-json-file* (merge-pathnames "spec.json"
                                          (system-source-directory "clcm")))

(defun test ()
  (%test #'cm->html))

;; https://gist.github.com/nfunato/01b5ec7bb7b60be18a0920b86783fa3a
(defun %test (fn)
  (let ((test-cases (decode-json-from-source *spec-json-file*))
        (sect-tbl   (make-hash-table :test #'equal))
        (sect-names '()))
    (labels ((json-attr (key tc)
               (cdr (or (assoc key tc :test #'eq)
                        (error "json-attr"))))
             (execute-test (tc)
               (cons (json-attr :section tc)
                     (string= (json-attr :html tc)
                              (funcall fn (json-attr :markdown tc)))))
             (get-sect-rec (name)
               (or (gethash name sect-tbl)
                   (progn (push name sect-names)
                          (setf (gethash name sect-tbl) (cons 0 0))))))
      (loop :for test-case :in test-cases
            :for (sect-name . passed-p) := (execute-test test-case)
            :for sect-rec := (get-sect-rec sect-name)
            :do  (incf (cdr sect-rec))
                 (when passed-p (incf (car sect-rec))))
      (loop :for sect-name :in (reverse sect-names)
            :for (sect-passed . sect-total) := (get-sect-rec sect-name)
            :for ok/10 := (floor (* 10 (/ sect-passed sect-total)))
            :for ok-bar := (make-string ok/10 :initial-element #\*)
            :for ng-bar := (make-string (- 10 ok/10) :initial-element #\-)
            :do  (format t "~A:~42T~A~46T/ ~A~52T ~A~A~%"
                         sect-name sect-passed sect-total ok-bar ng-bar)
            :summing sect-passed :into passed
            :summing sect-total  :into total
            :finally (format t
                             "~%TOTAL:~42T~A~46T/ ~A test cases (~,2F%)~%"
                             passed
                             total
                             (* (/ passed total) 100))))))

(defun test-for (num)
  (let* ((test-data (decode-json-from-source *spec-json-file*))
         (test-case (nth (1- num) test-data))
         (result (cm->html (cdr (assoc :markdown test-case)))))
    (if (string= (cdr (assoc :html test-case)) result)
        (values t (format nil "OK~%"))
        (values nil (format nil "NG~%~A~%" result)))))

(defun test-range (start end)
  (loop :for ind :from start :to end
        :do (format t "~A: ~A~%" ind (clcm/test:test-for ind))))

(defun test-section (key)
  (case key
    (:tabs (test-range 1 11))
    (:precedence (test-range 12 12))
    (:thematic-breaks (test-range 13 31))
    (:atx-headings (test-range 32 49))
    (:setext-headings (test-range 50 76))
    (:indented-code-blocks (test-range 77 88))
    (:fenced-code-blocks (test-range 89 117))
    (:html-blocks (test-range 118 160))
    (:link-reference-definitions (test-range 161 188))
    (:paragraphs (test-range 189 196))
    (:blank-lines (test-range 197 197))
    (:block-quotes (test-range 198 222))
    (:list-items (test-range 223 270))
    (:lists (test-range 271 296))
    ;;
    (:entity-and-numeric-character-references (test-range 311 327))
    (:code-spans (test-range 328 349))
    (:autolinks (test-range 590 608))))
